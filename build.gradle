buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "SpongePowered"
            url  = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath "org.spongepowered:mixingradle:0.4-SNAPSHOT"
    }
}

apply plugin: "com.github.johnrengelman.shadow";
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

group = 'org.faxhax'
version = '1.0.1'
archivesBaseName = 'faxhax'

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
    version = '1.12.2-14.23.5.2768'
    runDir = 'run'
    mappings = 'snapshot_20180814'
    coreMod = 'org.faxhax.faxhax.mixin.FaxMixinLoader'
    makeObfSourceJar = true
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        name = 'impactdevelopment-repo'
        url = 'https://impactdevelopment.github.io/maven/'
    }
    maven {
        name = "spongepowered-repo"
        url  = "https://repo.spongepowered.org/maven"
    }
    maven {
        name = 'lukflug'
        url = 'https://lukflug.github.io/maven'
    }
    maven {
        name = "jitpack.io"
        url  = "https://jitpack.io"
    }
}

dependencies {
    // HWID
    implementation 'com.github.oshi:oshi-core:5.5.0'

    // Mixin
    compile('org.spongepowered:mixin:0.7.4-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    // ClickGUI
    compile('com.lukflug:panelstudio:0.1.8')
    compile('com.lukflug:panelstudio-mc12:0.1.8')

    // Discord RPC
    compile 'club.minnced:java-discord-rpc:2.0.1'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    dependencies {
        include(dependency("club.minnced:java-discord-rpc:2.0.1"))
        include(dependency("com.github.oshi:oshi-core:5.5.0"))
        include(dependency("com.lukflug:panelstudio-mc12"))
        include(dependency("com.lukflug:panelstudio"))
        include(dependency("org.spongepowered:mixin"))
        include(dependency('cabaletta:baritone-api'))
    }
    exclude 'dummyThing'
    exclude 'LICENSE.txt'
    classifier = ''
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.momentum.refmap.json'
}

jar.manifest.attributes(
        'FMLCorePlugin': 'org.faxhax.faxhax.mixin.FaxMixinLoader',
        'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
        'MixinConfigs': 'mixins.faxhax.json',
        'FMLCorePluginContainsFMLMod': true,
        'FMLAT': 'faxhax_at.cfg',
        'ForceLoadAsMod': true,
        'TweakOrder': 0
)

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

build.dependsOn(shadowJar)