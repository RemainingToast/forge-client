buildscript {
    repositories {
        jcenter()
        maven {
            name = "SpongePowered"
            url  = "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath "org.spongepowered:mixingradle:0.4-SNAPSHOT"
    }
}

apply plugin: "com.github.johnrengelman.shadow";
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

group = 'org.faxhax'
version = '1.0.1'
archivesBaseName = 'FaxHax'

minecraft {
    version = project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion
    makeObfSourceJar = true
}

repositories {
    mavenCentral()
    maven {
        name = "spongepowered-repo"
        url  = "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        name = 'lukflug'
        url = 'https://lukflug.github.io/maven'
    }
    maven {
        name = "jitpack.io"
        url  = "https://jitpack.io"
    }
}

dependencies {
    compile('com.lukflug:panelstudio:0.1.8')
    compile('com.lukflug:panelstudio-mc12:0.1.8')
    compile "com.github.ionar2:Alpine:1.9f"
    compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: "commons-io"
        exclude module: "guava"
        exclude module: "gson"
    }

}

shadowJar {
    dependencies {
        include(dependency("com.lukflug:panelstudio"))
        include(dependency("com.lukflug:panelstudio-mc12"))
        include(dependency("com.github.ionar2:Alpine"))
        include(dependency("org.spongepowered:mixin"))
    }
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.faxhax.refmap.json"
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath   = sourceSets.main.compileClasspath
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
//        exclude "mcmod.info"
    }
}

jar {
    manifest {
        attributes (
                "FMLCorePluginContainsFMLMod" : "true",
                "ForceLoadAsMod"              : "true",
                "FMLCorePlugin"               : "org.faxhax.faxhax.mixin.FaxMixinLoader",
                "MixinConfigs"                : "mixins.faxhax.json",
                "tweakClass"                  : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"                  : 0,
                "FMLAT"                       : "faxhax_at.cfg"
        )
    }
}

build.dependsOn(shadowJar)
